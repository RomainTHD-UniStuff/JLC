-- Program -------------------------------------------------

entrypoints Prog ;

Program.        Prog    ::= [TopDef] ;

TopFnDef.       TopDef  ::= FuncDef ;
TopClsDef.      TopDef  ::= ClassDef ;

separator nonempty TopDef "" ;

-- Classes -------------------------------------------------

ClsDef.         ClassDef    ::= "class" Ident ClassInheritance "{" [Member] "}" ;

HBase.          ClassInheritance    ::= ;
HExtends.       ClassInheritance    ::= "extends" Ident ;

FnMember.       Member  ::= FuncDef ;
AttrMember.     Member  ::= Type Ident ";" ;

separator Member "" ;

-- Functions -----------------------------------------------

FnDef.          FuncDef ::= Type Ident "(" [Arg] ")" Blk ;

Argument.       Arg     ::= Type Ident ;

separator Arg "," ;

-- Statements ----------------------------------------------

Block.          Blk     ::= "{" [Stmt] "}" ;

Empty.          Stmt    ::= ";" ;

BStmt.          Stmt    ::= Blk ;

Decl.           Stmt    ::= Type [Item] ";" ;

NoInit.         Item    ::= Ident ;
Init.           Item    ::= Ident "=" Expr ;
separator nonempty Item "," ;

Ass.            Stmt    ::= Expr "=" Expr ";" ;

Incr.           Stmt    ::= Ident "++" ";" ;
Decr.           Stmt    ::= Ident "--" ";" ;

Ret.            Stmt    ::= "return" Expr ";" ;
VRet.           Stmt    ::= "return" ";" ;

-- Shift-reduce
CondElse.       Stmt    ::= "if" "(" Expr ")" Stmt "else" Stmt ;
Cond.           Stmt    ::= "if" "(" Expr ")" Stmt ;

While.          Stmt    ::= "while" "(" Expr ")" Stmt ;
For.            Stmt    ::= "for" "(" Type Ident ":" Expr ")" Stmt ;

SExp.           Stmt    ::= Expr ";" ;

separator Stmt "" ;

-- Types ---------------------------------------------------

Int.            BaseType    ::= "int" ;
Doub.           BaseType    ::= "double" ;
Bool.           BaseType    ::= "boolean" ;
Void.           BaseType    ::= "void" ;
Class.          BaseType    ::= Ident ;

-- Shift-reduce
TType.          Type    ::= BaseType [Dim] ;

-- Expressions ---------------------------------------------

-- Shift-reduce
ENull.          Expr9   ::= "(" Ident ")" "null" ;

EVar.           Expr8   ::= Ident ;

ELitInt.        Expr8   ::= Integer ;
ELitDoub.       Expr8   ::= Double;
ELitTrue.       Expr8   ::= "true" ;
ELitFalse.      Expr8   ::= "false" ;
EString.        Expr8   ::= String ;

EApp.           Expr7   ::= Expr7 "(" [Expr] ")" ;
EDot.           Expr7   ::= Expr6 "." Ident ;

ENew.           Expr6   ::= "new" BaseType MaybeNonInitArray [Index] ;
EIndex.         Expr6   ::= Expr7 Index [Index] ;

Neg.            Expr5   ::= "-" Expr6 ;
Not.            Expr5   ::= "!" Expr6 ;

EMul.           Expr4   ::= Expr4 MulOp Expr5 ;
EAdd.           Expr3   ::= Expr3 AddOp Expr4 ;
ERel.           Expr2   ::= Expr2 RelOp Expr3 ;

EAnd.           Expr1   ::= Expr2 "&&" Expr1 ;
EOr.            Expr    ::= Expr1 "||" Expr ;

coercions Expr 9 ;
separator Expr "," ;

-- Dimensions ----------------------------------------------

NonInitArray.   MaybeNonInitArray ::= "[]" ;
InitArray.      MaybeNonInitArray ::= ;

SIndex.         Index   ::= "[" Expr "]" ;
separator Index "" ;

DimenT.         Dim     ::= "[]" ;
separator Dim "" ;

-- Operators -----------------------------------------------

Plus.           AddOp   ::= "+" ;
Minus.          AddOp   ::= "-" ;

Times.          MulOp   ::= "*" ;
Div.            MulOp   ::= "/" ;
Mod.            MulOp   ::= "%" ;

LTH.            RelOp   ::= "<" ;
LE.             RelOp   ::= "<=" ;
GTH.            RelOp   ::= ">" ;
GE.             RelOp   ::= ">=" ;
EQU.            RelOp   ::= "==" ;
NE.             RelOp   ::= "!=" ;

-- Comments ------------------------------------------------

comment "#" ;
comment "//" ;
comment "/*" "*/" ;
