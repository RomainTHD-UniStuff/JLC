-- programs ------------------------------------------------

entrypoints Prog ;

Program.   Prog ::= [TopDef] ;

TopFnDef.  TopDef ::= FuncDef ;

TopClsDef. TopDef ::= ClassDef ;

separator nonempty TopDef "" ;

-- classes -------------------------------------------------

ClsDef.    ClassDef ::= "class" Ident ClassInheritance "{" [Member] "}" ;

HBase.     ClassInheritance ::= ;

HExtends.  ClassInheritance ::= "extends" Ident ;

FnMember.  Member ::= FuncDef ;

AttrMember. Member ::= Type Ident ";" ;

-- functions -----------------------------------------------

FnDef.     FuncDef ::= Type Ident "(" [Arg] ")" Blk ;

separator nonempty Member "" ;

Argument.  Arg ::= Type Ident;

separator  Arg "," ;

-- statements ----------------------------------------------

Block.     Blk ::= "{" [Stmt] "}" ;

separator  Stmt "" ;

Empty.     Stmt ::= ";" ;

BStmt.     Stmt ::= Blk ;

Decl.      Stmt ::= Type [Item] ";" ;

NoInit.    Item ::= Ident ;

Init.      Item ::= Ident "=" Expr ;

separator nonempty Item "," ;

Ass.       Stmt ::= Ident "=" Expr  ";" ;

Incr.      Stmt ::= Ident "++"  ";" ;

Decr.      Stmt ::= Ident "--"  ";" ;

Ret.       Stmt ::= "return" Expr ";" ;

VRet.      Stmt ::= "return" ";" ;

Cond.      Stmt ::= "if" "(" Expr ")" Stmt ;

CondElse.  Stmt ::= "if" "(" Expr ")" Stmt "else" Stmt ;

While.     Stmt ::= "while" "(" Expr ")" Stmt ;

For.       Stmt ::= "for" "(" Type Ident ":" Expr ")" Stmt ;

SExp.      Stmt ::= Expr ";" ;

-- Types ---------------------------------------------------

Int.       Type ::= "int" ;

Doub.      Type ::= "double" ;

Bool.      Type ::= "boolean" ;

Void.      Type ::= "void" ;

-- Array.     Type ::= Type [Dim] ;

Class.     Type ::= Ident ;

-- separator  Type "," ;

-- Expressions ---------------------------------------------

ENull.     Expr7 ::= "(" Ident ")" "null" ;

EVar.      Expr6 ::= Ident ;

ELitInt.   Expr6 ::= Integer ;

ELitDoub.  Expr6 ::= Double;

ELitTrue.  Expr6 ::= "true" ;

ELitFalse. Expr6 ::= "false" ;

ESelf.     Expr6 ::= "self" ;

EApp.      Expr6 ::= Ident "(" [Expr] ")" ;

EString.   Expr6 ::= String ;

-- Works because we don't allow raw access to class attributes, only function
--  calls are allowed
EDot.      Expr6 ::= Expr6 "." Ident "(" [Expr] ")" ;

-- EIndex.    Expr6 ::= Expr6 [Index] ;

ENew.      Expr6 ::= "new" Type ;
-- ENew.      Expr6 ::= "new" Type [Index] ;

Neg.       Expr5 ::= "-" Expr6 ;

Not.       Expr5 ::= "!" Expr6 ;

EMul.      Expr4 ::= Expr4 MulOp Expr5 ;

EAdd.      Expr3 ::= Expr3 AddOp Expr4 ;

ERel.      Expr2 ::= Expr2 RelOp Expr3 ;

EAnd.      Expr1 ::= Expr2 "&&" Expr1 ;

EOr.       Expr ::= Expr1 "||" Expr ;

coercions  Expr 7 ;

separator  Expr "," ;

-- dimensions ----------------------------------------------

-- SIndex.    Index ::= "[" Expr "]" ;

-- separator  Index "" ;

-- SDim.      Dim ::= "[" "]" ;

-- separator  Dim "" ;

-- operators -----------------------------------------------

Plus.      AddOp ::= "+" ;

Minus.     AddOp ::= "-" ;

Times.     MulOp ::= "*" ;

Div.       MulOp ::= "/" ;

Mod.       MulOp ::= "%" ;

LTH.       RelOp ::= "<" ;

LE.        RelOp ::= "<=" ;

GTH.       RelOp ::= ">" ;

GE.        RelOp ::= ">=" ;

EQU.       RelOp ::= "==" ;

NE.        RelOp ::= "!=" ;

-- comments ------------------------------------------------

comment    "#" ;

comment    "//" ;

comment    "/*" "*/" ;
